# AOC 2024 Day 8, Part 2

# npx dusa src/8b.dusa -f data/day8-test.json -cantinode

#builtin INT_PLUS plus
#builtin INT_MINUS minus
#builtin INT_TIMES times

width is W :-
    root 0 is Root,
    field Root 0 is FirstLine,
    length FirstLine is W.
height is H :-
    root 0 is Root,
    length Root is H.
tower X Y is Ch :-
    root 0 is Root,
    field Root Y is Line,
    field Line X is Ch, Ch != ".".

resonance X2 Y2 DX DY :-
    tower X1 Y1 is A,
    tower X2 Y2 is A,
    tuple X1 Y1 != tuple X2 Y2,
    DX == minus X2 X1,
    DY == minus Y2 Y1.

antinode X Y :-
    resonance X Y DX DY,
    0 <= X, X < width,
    0 <= Y, Y < height.
resonance (plus X DX) (plus Y DY) DX DY :-
    resonance X Y DX DY,
    0 <= X, X < width,
    0 <= Y, Y < height.